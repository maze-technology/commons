plugins {
  id("jacoco")
  id("checkstyle")
  id("java-library")
  id("maven-publish")
  id("com.github.spotbugs") version "6.4.7"
  id("org.springframework.boot") version "4.0.2"
  id("io.spring.dependency-management") version "1.1.7"
  id("io.freefair.lombok") version "9.1.0"
}

ext {
  junitVersion                = "6.0.1"
  wiremockVersion             = "3.0.1"
  mapStructVersion            = "1.6.3"
  springBootVersion           = "4.0.2"
  protoValidateVersion        = "1.0.1"
  grpcSpringBootVersion       = "0.12.0"
  jakartaValidationVersion    = "3.1.1"
  jakartaAnnotationVersion    = "3.0.0"
  jakartaPersistenceVersion   = "3.2.0"
  lombokVersion               = "1.18.42"
  hikariCpVersion             = "7.0.2"
  h2Version                   = "2.4.240"
  spotbugsVersion             = "4.9.8"
  googleCommonProtosVersion   = "2.63.1"
}

repositories {
  mavenLocal()
  mavenCentral()
  gradlePluginPortal()

  maven {
    url = "https://repo.spring.io/milestone"
  }

  maven {
    url = "https://repo.spring.io/release"
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
    mavenBom "org.junit:junit-bom:$junitVersion"
  }
}

dependencies {
  // Spring Boot
  implementation "org.springframework.boot:spring-boot"
  implementation "org.springframework.data:spring-data-jpa"

  // HikariCP
  implementation "com.zaxxer:HikariCP:$hikariCpVersion"

  // ProtobValidate
  implementation "build.buf:protovalidate:$protoValidateVersion"

  // Spring Boot GRPC
  implementation "org.springframework.grpc:spring-grpc-spring-boot-starter:$grpcSpringBootVersion"
  implementation platform("org.springframework.grpc:spring-grpc-dependencies:$grpcSpringBootVersion")

  implementation "com.google.api.grpc:proto-google-common-protos:$googleCommonProtosVersion"

  // Jakarta
  implementation "jakarta.validation:jakarta.validation-api:$jakartaValidationVersion"
  implementation "jakarta.annotation:jakarta.annotation-api:$jakartaAnnotationVersion"
  implementation "jakarta.persistence:jakarta.persistence-api:$jakartaPersistenceVersion"

  // MapStruct
  implementation "org.mapstruct:mapstruct:$mapStructVersion"
  annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"

  // Lombok
  compileOnly "org.projectlombok:lombok:$lombokVersion"
  annotationProcessor "org.projectlombok:lombok:$lombokVersion"

  // SpotBugs
  compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"
  spotbugs "com.github.spotbugs:spotbugs:$spotbugsVersion"
  spotbugs "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"

  // Test Framework
  testImplementation "org.springframework.boot:spring-boot-starter-test"
  testImplementation "org.junit.jupiter:junit-jupiter"
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testImplementation "org.junit.jupiter:junit-jupiter-engine"

  // Mock Framework
  testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:${wiremockVersion}"

  // H2 Test Database
  testImplementation "com.h2database:h2:$h2Version"
}

checkstyle {
  toolVersion = "12.2.0"
  config = resources.text.fromFile("$rootDir/config/checkstyle/checkstyle.xml")
  maxWarnings = 0
  ignoreFailures = false
}

import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort

spotbugs {
  projectName = name
  maxHeapSize = "512m"
  showProgress = false
  ignoreFailures = false
  showStackTraces = true
  effort = Effort.valueOf("DEFAULT")
  reportLevel = Confidence.valueOf("DEFAULT")
  extraArgs = [
    "-auxclasspath",
    sourceSets.main.runtimeClasspath.filter {
      !it.absolutePath.contains("/generated/")
    }.asPath
  ]
}

spotbugsMain {
  dependsOn compileJava

  classes = fileTree("${buildDir}/classes/java/main") {
    exclude "tech/maze/dtos/**"
  }

  reports {
    html {
      required = true
      outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
      stylesheet = "fancy-hist.xsl"
    }
  }
}

test {
  useJUnitPlatform()

  testLogging {
    events = ["passed", "skipped", "failed"]
  }

  reports {
    junitXml.required = true
  }

  finalizedBy(jacocoTestReport)
}

jacocoTestReport {
  dependsOn delombok
  sourceSets(sourceSets.main)
  executionData = fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: [
        "tech/maze/dtos/*",
      ])
    }))
  }

  reports {
    xml.required = true
    csv.required = false
  }
}

jacocoTestCoverageVerification {
  sourceSets(sourceSets.main)
  executionData = fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: [
        "tech/maze/dtos/*",
      ])
    }))
  }

  violationRules {
    rule {
      limit {
        minimum = 0.8
      }
    }
  }
}

testing {
  suites {
    integrationTest(JvmTestSuite) {
      dependencies {
        implementation project()
        // Use the provided integrationTestImplementation configuration
      }
    }
  }
}

tasks.withType(Javadoc).configureEach {
  source = source.filter {
    !it.absolutePath.startsWith("${buildDir.absolutePath}/generated/")
  }

  dependsOn delombok
}

tasks.named("checkstyleMain", Checkstyle) {
  source = source.filter { !it.absolutePath.contains("tech/maze/dtos") }
}

sourceSets {
  main {
    java.srcDir "$buildDir/generated/source"
  }
}

bootJar {
  enabled = false
}

jar {
  enabled = true
  archiveClassifier = ""

  manifest {
    attributes(
      'Implementation-Title': project.name,
      'Implementation-Version': project.version,
    )
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }

  repositories {
    maven {
      name = "github"
      url = "https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY")

      credentials {
        username = System.getenv("GITHUB_USERNAME")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}

// Task to print Maven coordinates for CI/CD workflows
tasks.register("printCoordinates") {
  doLast {
    println("group=${project.group}")
    println("artifactId=${project.name}")
  }
}
